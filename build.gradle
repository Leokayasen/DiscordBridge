import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.bitwave'
version = '1.2.5'
description = 'A BitWave Utility Plugin for Minecraft 1.21.8 Paper Servers'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT'
    implementation 'net.dv8tion:JDA:5.1.1'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.register('buildPlugin', ShadowJar) {
    archiveClassifier.set('')
    from(sourceSets.main.output)
    configurations = [project.configurations.runtimeClasspath]

    // Relocate JDA to prevent conflicts with other plugins that might also use it
    relocate 'net.dv8tion.jda', 'com.bitwave.discordbridge.libs.jda'
    relocate 'org.apache.commons', 'com.bitwave.discordbridge.libs.apache'

    manifest {
        attributes(
                'Main-Class': 'com.bitwave.discordbridge.DiscordBridge'
        )
    }
}
